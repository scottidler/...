#!/bin/zsh

. ~/.shell-aliases

get-aliases() {
    alias | grep "$1" | cut -d'=' -f1
}

remove-comment() {
    local WORDCHARS=' #!space'
    zle backward-delete-word
}

sudoify() {
    CMD="${BUFFER%% *}"
    ARGS="${BUFFER#* }"
    ABS="$(which "$CMD")"
    [[ -n "$ABS" ]]; CMD="$ABS"
    BUFFER="sudo -E $CMD ${ARGS% !}"
}

expand-alias-space() {
    if [ -n "$DEBUG_EXPAND_ALIAS" ]; then
        PS4=':${LINENO}+'
        set -x
    fi
    no_spaces=($(get-aliases "#!space"))
    ignores=($(get-aliases "#!expand"))
    if [[ -n "$EXPAND_ALIAS" ]]; then
        [[ $LBUFFER =~ "\<(${(j:|:)no_spaces})\$" ]]; blank=$?
        if [[ ! $LBUFFER =~ "\<(${(j:|:)ignores})\$" ]]; then
            zle _expand_alias
        fi
        zle self-insert
        if [[ "$blank" == "0" ]]; then
            remove-comment
        fi
    fi
}

zle -N expand-alias-space

bindkey " " expand-alias-space
bindkey -M isearch " " magic-space

expand-alias-accept-line() {
    if [ -n "$DEBUG_EXPAND" ]; then
        PS4=':${LINENO}+'
        set -x
    fi
    no_spaces=($(get-aliases "#!space"))
    ignores=($(get-aliases "#!expand"))
    if [[ -n "$EXPAND_ALIAS" ]]; then
        [[ $BUFFER =~ "\<(${(j:|:)no_spaces})\$" ]]; blank=$?
        if [[ ! $BUFFER =~ "\<(${(j:|:)ignores})\$" ]]; then
            zle _expand_alias
        fi
        if [[ "$BUFFER" == *"!" ]]; then
            sudoify
        fi
        if [[ "$blank" == "0" ]]; then
            remove-comment
        fi
    fi
    zle .accept-line
}

zle -N accept-line expand-alias-accept-line
